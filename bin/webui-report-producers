#!/usr/bin/perl -w

use utf8;
use strict;
use EOSN::Validate::Report;
use List::Util qw(shuffle);
use Date::Format qw(time2str);
use HTML::Entities;
use JSON qw(to_json);

binmode(STDOUT, ":utf8");

# --------------------------------------------------------------------------
# Main

my $report = EOSN::Validate::Report->new;
$report->get_report_options;

my $webpage = $report->webpage;
my $data = $report->data;
my $chain = $report->chain;
my $properties = $webpage->chain_properties ($chain);
my @classes = $report->classes;

foreach my $lang ($webpage->langs) {
	foreach my $content_type ($webpage->content_types) {
		foreach my $entry (@{$$data{producers}}) {
			my @section;

			my $producer = $$entry{regproducer}{owner};
			my $producer_name_html = encode_entities($$entry{info}{name} || $$entry{regproducer}{owner});
			foreach my $message (@{$$entry{messages}}) {
				my $icon = $report->sev_html (kind => $$message{kind}, class => $$message{class}, lang => $lang);
				my $class = $webpage->label (key => 'class_' . $$message{class}, lang => $lang);
				push (@section, {
					sprintf => "%4s, %11s, %s",
					data => [$$message{kind}, $$message{class}, $report->generate_message ($message, lang => $lang, content_type => $content_type)],
					html_data => [$icon, $class, $report->generate_message ($message, lang => $lang, content_type => $content_type)]
				});
			}

			my $title = $webpage->label (key => 'title_producers', lang => $lang);
			$title =~ s/%producer_name_html%/$producer_name_html/;
			$report->generate_report (lang => $lang, content_type => $content_type, report => [{rows => \@section}], title => $title, columns => 1, outfile => "producers/$producer", text => 1, json => 1, html => 1);
		}
	}
}

my @entries1;
my @entries2;
my @entries3;

foreach my $entry (shuffle (@{$$data{producers}})) {
	my $is_paid = $$entry{info}{is_paid};

	my $check = 0;
	my $msgsummary = $$entry{message_summary};
	foreach my $key (@classes) {
		if (exists $$msgsummary{$key} && $$msgsummary{$key} eq 'ok') {
			$check++;
		}
	}

	if ($check > 0) {
		push (@entries1, $entry);
	} elsif ($is_paid) {
		push (@entries2, $entry);
	} else {
		push (@entries3, $entry);
	}
}

foreach my $lang ($webpage->langs) {
	my @report;
	push (@report, "<div>\n");
	foreach my $entry (@entries1, @entries2, @entries3) {
		my $producer = $$entry{regproducer}{owner};
		my $rank = $$entry{info}{rank};
		my $is_standby = $$entry{info}{is_standby};
		my $is_top_21 = $$entry{info}{is_top_21};
		my $producer_name_html = encode_entities($$entry{info}{name} || $$entry{regproducer}{owner});
		my $country = $$entry{info}{country_alpha2};

		my $logo = $report->bp_logo ($entry);
		my %results;

		my %msgsummary = %{$$entry{message_summary}} if (ref $$entry{message_summary});

		foreach my $key (@classes) {
			if (defined $msgsummary{$key} && ($msgsummary{$key} eq 'ok' || $msgsummary{$key} eq 'info')) {
				$msgsummary{$key} = $report->sev_html (kind => "class_$key", class => $key, lang => $lang);
			} else {
				$msgsummary{$key} = $report->sev_html (kind => 'none', class => $key, lang => $lang);
			}
		}

		my $selected = '';
		if ($is_top_21) {
			$selected = $report->sev_html (kind => 'bp_top21', class => undef, lang => $lang);
		} elsif ($is_standby) {
			$selected = $report->sev_html (kind => 'bp_standby', class => undef, lang => $lang);
		} else {
			$selected = $report->sev_html (kind => 'bp_other', class => undef, lang => $lang);
		}

		my $flag = '';
		if ($country) {
			$flag = $report->flag_html ($country);
		}

		push (@report, "<div id=\"bp_$producer\" class=\"scorecard\" onclick=\"window.location.href='$producer.html'\">\n");
		push (@report, "<div class=\"scorecard-title\"><span class=bp_name>$producer_name_html</span></div>\n");
		push (@report, "<div>\n");
		push (@report, "  <div style=\"display: inline-block; vertical-align: middle; width: 32px\">$logo</div>\n");
		push (@report, "  <div style=\"display: inline-block; vertical-align: middle; width: 28px\">$flag</div>\n");
		push (@report, "  <div style=\"display: inline-block; vertical-align: middle; width: 32px; height: 32px\">$selected</div>\n");
		push (@report, "</div>\n");
		push (@report, "<div>\n");
		foreach my $key (@classes) {
			my $class_key = "class_$key";
			next if (! $$properties{$class_key});
			next if (! $msgsummary{$key});
			push (@report, "  <div style=\"display: inline-block; vertical-align: middle; width: 32px\">$msgsummary{$key}</div>\n");
		}
		push (@report, "</div>\n");
		push (@report, "</div>\n");
	}
	push (@report, "</div>\n");

	my @filter;
	foreach my $entry (@entries1, @entries2, @entries3) {
		my $producer = $$entry{regproducer}{owner};
		my $rank = $$entry{info}{rank};
		my $is_standby = $$entry{info}{is_standby};
		my $is_top_21 = $$entry{info}{is_top_21};

		my %results;
		$results{name} = $producer;

		if ($is_top_21) {
			push (@{$results{tags}}, 'top21=y');
			push (@{$results{tags}}, 'paid=n');
			push (@{$results{tags}}, 'other=n');
		} elsif ($is_standby) {
			push (@{$results{tags}}, 'top21=n');
			push (@{$results{tags}}, 'paid=y');
			push (@{$results{tags}}, 'other=n');
		} else {
			push (@{$results{tags}}, 'top21=n');
			push (@{$results{tags}}, 'paid=n');
			push (@{$results{tags}}, 'other=y');
		}

		foreach my $key (@classes) {
			if (exists $$entry{message_summary}{$key} && (($$entry{message_summary}{$key} eq 'ok') || ($$entry{message_summary}{$key} eq 'info'))) {
					push (@{$results{tags}}, $key . '=y');
				} else {
					push (@{$results{tags}}, $key . '=n');
				}
		}

		push (@filter, \%results);
	}

	my $final_result = to_json ({
		meta => {generated_at => time2str ("%C", time), maintainer => 'matthew@eosnation.io'},
		producers => \@filter
	}, {
		pretty => 0,
		canonical => 1
	});

	my @form;
	push (@form, "<div>\n");

	push (@form, checkbox (
		name => 'top21',
		label => $report->sev_html (kind => 'bp_top21', class => undef, lang => $lang, color => 'bw') . ' ' . $webpage->label (key => 'bp_top21', lang => $lang),
		lang => $lang
	));
	push (@form, checkbox (
		name => 'paid',
		label => $report->sev_html (kind => 'bp_standby', class => undef, lang => $lang, color => 'bw') . ' ' . $webpage->label (key => 'bp_standby', lang => $lang),
		lang => $lang
	));
	push (@form, checkbox (
		name => 'other',
		label => $report->sev_html (kind => 'bp_other', class => undef, lang => $lang, color => 'bw') . ' ' . $webpage->label (key => 'bp_other', lang => $lang),
		lang => $lang
	));

	foreach my $key (@classes) {
		my $class_key = "class_$key";
		my $classl_key = "class_${key}_l";
		next if (! $$properties{$class_key});
		push (@form, checkbox (
			name => $key,
			label => $report->sev_html (kind => $class_key, class => undef, lang => $lang, color => 'bw') . ' '. $webpage->label (key => $classl_key, lang => $lang),
			lang => $lang
		));
	}

	push (@form, "</div>\n");

	unshift (@report, @form);

	push (@report, "<script>\nvar filterData = $final_result\n</script>\n");

	$report->write_report_thtml (lang => $lang, content => \@report, outfile => "producers/index");
}

sub checkbox {
	my (%options) = @_;

	my $name = $options{name};
	my $label = $options{label};
	my $lang = $options{lang};

	my $string = '';
	$string .= "<div class=\"filtergroup\">\n";
	$string .= "$label<br>\n";
	$string .= " <label class=\"radio\"><input class=\"filter\" type=\"radio\" name=\"$name\" value=\"${name}=y\" onClick=\"clickFilter()\"> " . $webpage->label (key => 'filter_yes', lang => $lang) . "</label>\n";
	$string .= " <label class=\"radio\"><input class=\"filterx\" type=\"radio\" name=\"$name\" checked onClick=\"clickFilter()\"> " . $webpage->label (key => 'filter_na', lang => $lang) ."</label>\n";
	$string .= " <label class=\"radio\"><input class=\"filter\" type=\"radio\" name=\"$name\" value=\"${name}=n\" onClick=\"clickFilter()\"> " . $webpage->label (key => 'filter_no', lang => $lang) . "</label>\n";
	$string .= "</div>\n";
	return $string;
}
